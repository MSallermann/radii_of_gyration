import polars as pl
from pathlib import Path
import itertools


configfile: "./config.yml"


DATA_BASE = pl.read_parquet(config["database_file"])


def get_plddts(wc):
    accession = samples[wc.sample]["accession"]
    return DATA_BASE.filter(pl.col("accession") == accession)["plddts"][0]


def get_cif(wc):
    accession = samples[wc.sample]["accession"]
    return DATA_BASE.filter(pl.col("accession") == accession)["cif_text"][0]


TEMPERATURE = config["temperature"]
IONIC_STRENGTH = config["ionic_strength"]
N_STEPS = config["n_steps"]
TIMESTEP = config["timestep"]
MINIMUM_DOMAIN_LENGTH = config["minimum_domain_length"]
MINIMUM_IDR_LENGTH = config["minimum_idr_length"]
THRESHOLD = config["threshold"]

LAMMPS_BIN = config["lammps_binary"]

RG_SKIP = config["rg_skip"]

accessions = DATA_BASE["accession"]

thresholds = [25, 50, 75, 100]

samples = {}
for acc, thresh in itertools.product(accessions, thresholds):
    key = f"{acc}_{thresh}"
    samples[key] = {"accession": acc, "thresh": thresh}


rule all:
    input:
        [f"results/aggregated_rg.csv" for acc in accessions],


rule prepare_coarse_grained_inputs:
    localrule: True
    input:
        template_file=ancient(
            Path(workflow.source_path("templates/rg_script.lmp.jinja"))
        ),
    output:
        directory("results/lammps_files/{sample}"),
    params:
        temp=TEMPERATURE,
        ionic_strength=IONIC_STRENGTH,
        n_steps=N_STEPS,
        timestep=TIMESTEP,
        threshold=lambda wc: samples[wc.sample]["thresh"],
        minimum_domain_length=MINIMUM_DOMAIN_LENGTH,
        minimum_idr_length=MINIMUM_IDR_LENGTH,
        plddts=get_plddts,
        cif_text=get_cif,
    script:
        workflow.source_path("scripts/coarse_grain.py")


rule run_lammps:
    input:
        "results/lammps_files/{sample}",
    output:
        folder=directory("results/lammps_runs/{sample}"),
        gyration="results/lammps_runs/{sample}/gyration.out",
    resources:
        nodes=1,
        n_tasks_per_node=1,
        slurm_out=lambda wc: f"results/lammps_files/{wc.sample}slurm-%j.out",
        slurm_err=lambda wc: f"results/lammps_files/{wc.sample}slurm-%j.err",
    shell:
        "cp -r {input}/* {output.folder}\n"
        "cd {output.folder}\n"
        f"srun {LAMMPS_BIN} -in script.lmp\n"


rule average_radius_of_gyration:
    input:
        "results/lammps_runs/{sample}/gyration.out",
    output:
        rg_json="results/rg/{sample}/rg.json",
    params:
        n_skip=int(RG_SKIP),
    script:
        workflow.source_path("scripts/average_radius_of_gyration.py")


rule aggregate_rg:
    input:
        [f"results/rg/{sample}/rg.json" for sample in samples],
    params:
        add_columns={
            "accession": [samples[sample]["accession"] for sample in samples],
            "threshold": [samples[sample]["thresh"] for sample in samples],
        },
        ignore_columns=["file"],
    output:
        "results/aggregated_rg.csv",
    script:
        workflow.source_path("scripts/aggregate_json.py")
